# parameterized SPARQL query, should be given the title of an article on en-wp
# modified from https://github.com/Kyrkosok/web-client/blob/master/queries/church-specific/wp-intro.rq
SELECT ?extract WHERE {
  BIND("{{.}}" AS ?title)
  SERVICE wikibase:mwapi {
    bd:serviceParam wikibase:endpoint "en.wikipedia.org";
                    wikibase:api "Generator";
                    wikibase:limit "once";
                    mwapi:generator "allpages";
                    mwapi:gapfrom ?title;
                    mwapi:gapto ?title;
                    mwapi:gaplimit 1;
                    mwapi:prop "extracts";
                    mwapi:exsentences "3".
    ?rawExtract wikibase:apiOutput "extract/text()".
    ?ns wikibase:apiOutput "@ns".
    ?pageid wikibase:apiOutput "@pageid". 
  }
  
  BIND(CONCAT(REPLACE(STR(?rawExtract), "<[^>]*>", ""), ' [...]') AS ?extract)
}